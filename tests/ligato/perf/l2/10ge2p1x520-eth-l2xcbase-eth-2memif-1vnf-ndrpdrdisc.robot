# Copyright (c) 2017 Cisco and/or its affiliates.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at:
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

*** Settings ***
| Resource | resources/libraries/robot/performance/performance_setup.robot
| ...
| Force Tags | 3_NODE_SINGLE_LINK_TOPO | PERFTEST | HW_ENV | NDRPDRDISC
| ... | NIC_Intel-X520-DA2 | ETH | L2XCFWD | BASE | L2XCBASE | KUBERNETES
| ... | 1VSWITCH | 1VNF
| ...
| Suite Setup | Set up 3-node performance topology with DUT's NIC model
| ... | L2 | Intel-X520-DA2
| ...
| Test Setup | Run Keywords
| ... | Apply Kubernetes resource on all duts | ${nodes} | kafka.yaml
| ... | AND | Apply Kubernetes resource on all duts | ${nodes} | etcd.yaml
| ...
#| Suite Teardown | Run Keywords | Tear down 3-node performance topology
#| ... | AND | Reset Kubernetes on all duts | ${nodes}
| ...
#| Test Teardown | Run Keywords
#| ... | Tear down performance discovery test | ${min_rate}pps | ${framesize}
#| ... | ${traffic_profile}
#| ... | AND | Delete Kubernetes resource on all duts | ${nodes}
| ...
| Documentation | *RFC2544: Pkt throughput L2XC test cases*
| ...
| ... | *[Top] Network Topologies:* TG-DUT1-DUT2-TG 3-node circular topology
| ... | with single links between nodes.
| ... | *[Enc] Packet Encapsulations:* Eth-IPv4 for L2 cross connect.
| ... | *[Cfg] DUT configuration:* DUT1 and DUT2 are configured with L2 cross-
| ... | connect. DUT1 and DUT2 tested with 2p10GE NIC X520 Niantic by Intel.
| ... | VNF Container is connected to VSWITCH container via Memif interface. All
| ... | containers is running same VPP version. Containers are deployed with
| ... | Kubernetes. Configuration is applied by vnf-agent.
| ... | *[Ver] TG verification:* TG finds and reports throughput NDR (Non Drop
| ... | Rate) with zero packet loss tolerance or throughput PDR (Partial Drop
| ... | Rate) with non-zero packet loss tolerance (LT) expressed in percentage
| ... | of packets transmitted. NDR and PDR are discovered for different
| ... | Ethernet L2 frame sizes using either binary search or linear search
| ... | algorithms with configured starting rate and final step that determines
| ... | throughput measurement resolution. Test packets are generated by TG on
| ... | links to DUTs. TG traffic profile contains two L3 flow-groups
| ... | (flow-group per direction, 253 flows per flow-group) with all packets
| ... | containing Ethernet header, IPv4 header with IP protocol=61 and static
| ... | payload. MAC addresses are matching MAC addresses of the TG node
| ... | interfaces.
| ... | *[Ref] Applicable standard specifications:* RFC2544.

*** Variables ***
| ${avg_imix_framesize}= | ${357.833}
# X520-DA2 bandwidth limit
| ${s_limit} | ${10000000000}
# Kubernetes profile
| ${kubernetes_profile} | eth-l2xcbase-eth-2memif-1vnf
# Traffic profile:
| ${traffic_profile} | trex-sl-3n-ethip4-ip4src254
# CPU settings
| ${system_cpus}= | ${1}
| ${vswitch_cpus}= | ${5}
| ${vnf_cpus}= | ${2}

*** Keywords ***
| Create Kubernetes VSWITCH startup config on all DUTs
| | [Arguments] | ${framesize} | ${wt} | ${rxq}
| | ${dut1_numa}= | Get interfaces numa node | ${dut1}
| | ... | ${dut1_if1} | ${dut1_if2}
| | ${dut2_numa}= | Get interfaces numa node | ${dut2}
| | ... | ${dut2_if1} | ${dut2_if2}
| | ${dut1_if1_pci}= | Get Interface PCI Addr | ${dut1} | ${dut1_if1}
| | ${dut1_if2_pci}= | Get Interface PCI Addr | ${dut1} | ${dut1_if2}
| | ${dut2_if1_pci}= | Get Interface PCI Addr | ${dut2} | ${dut2_if1}
| | ${dut2_if2_pci}= | Get Interface PCI Addr | ${dut2} | ${dut2_if2}
| | ${cpu_cnt}= | Evaluate | ${wt}+1
| | ${config}= | Run keyword | Create Kubernetes VSWITCH startup config
| | ... | node=${dut1} | cpu_cnt=${cpu_cnt} | cpu_node=${dut1_numa}
| | ... | cpu_skip=${system_cpus} | smt_used=${False}
| | ... | filename=/tmp/vswitch.conf | framesize=${framesize} | rxq=${rxq}
| | ... | if1=${dut1_if1_pci} | if2=${dut1_if2_pci}
| | ${config}= | Run keyword | Create Kubernetes VSWITCH startup config
| | ... | node=${dut2} | cpu_cnt=${cpu_cnt} | cpu_node=${dut2_numa}
| | ... | cpu_skip=${system_cpus} | smt_used=${False}
| | ... | filename=/tmp/vswitch.conf | framesize=${framesize} | rxq=${rxq}
| | ... | if1=${dut2_if1_pci} | if2=${dut2_if2_pci}

| Create Kubernetes VNF startup config on all DUTs
| | ${cpu_skip}= | Evaluate | ${vswitch_cpus}+${system_cpus}
| | ${dut1_numa}= | Get interfaces numa node | ${dut1}
| | ... | ${dut1_if1} | ${dut1_if2}
| | ${dut2_numa}= | Get interfaces numa node | ${dut2}
| | ... | ${dut2_if1} | ${dut2_if2}
| | ${config}= | Run keyword | Create Kubernetes VNF startup config
| | ... | node=${dut1} | cpu_cnt=${vnf_cpus} | cpu_node=${dut1_numa}
| | ... | cpu_skip=${cpu_skip} | smt_used=${False} | filename=/tmp/vnf.conf
| | ${config}= | Run keyword | Create Kubernetes VNF startup config
| | ... | node=${dut2} | cpu_cnt=${vnf_cpus} | cpu_node=${dut2_numa}
| | ... | cpu_skip=${cpu_skip} | smt_used=${False} | filename=/tmp/vnf.conf

| L2 Cross Connect Binary Search
| | [Arguments] | ${framesize} | ${min_rate} | ${wt} | ${rxq} | ${search_type}
| | Set Test Variable | ${framesize}
| | Set Test Variable | ${min_rate}
| | ${get_framesize}= | Set Variable If
| | ... | "${framesize}" == "IMIX_v4_1" | ${avg_imix_framesize} | ${framesize}
| | ${max_rate}= | Calculate pps | ${s_limit} | ${get_framesize}
| | ${binary_min}= | Set Variable | ${min_rate}
| | ${binary_max}= | Set Variable | ${max_rate}
| | ${threshold}= | Set Variable | ${min_rate}
| | Apply Kubernetes resource on all DUTs | ${nodes}
| | ... | ${kubernetes_profile}/sfc.yaml | $$TEST_NAME$$=${TEST NAME}
| | ... | $$VSWITCH_IF1$$=TenGigabitEthernet7/0/0
| | ... | $$VSWITCH_IF2$$=TenGigabitEthernet7/0/1
| | Create Kubernetes VSWITCH startup config on all DUTs | ${get_framesize}
| | ... | ${wt} | ${rxq}
| | Create Kubernetes CM from file on all DUTs | ${nodes} | name=vswitch-vpp-cfg
| | ... | key=vpp.conf | src_file=/tmp/vswitch.conf
| | Apply Kubernetes resource on all DUTs | ${nodes}
| | ... | ${kubernetes_profile}/vswitch.yaml
| | Create Kubernetes VNF startup config on all DUTs
| | Create Kubernetes CM from file on all DUTs | ${nodes} | name=vnf-vpp-cfg
| | ... | key=vpp.conf | src_file=/tmp/vnf.conf
| | Apply Kubernetes resource on all DUTs | ${nodes}
| | ... | ${kubernetes_profile}/vnf.yaml
| | Wait for Kubernetes PODs on all DUTs | ${nodes}
| | Describe Kubernetes PODs on all DUTs | ${nodes}
#| | Run Keyword If | '${search_type}' == 'NDR'
#| | ... | Find NDR using binary search and pps
#| | ... | ${framesize} | ${binary_min} | ${binary_max} | ${traffic_profile}
#| | ... | ${min_rate} | ${max_rate} | ${threshold}
#| | ... | ELSE IF | '${search_type}' == 'PDR'
#| | ... | Find PDR using binary search and pps
#| | ... | ${framesize} | ${binary_min} | ${binary_max} | ${traffic_profile}
#| | ... | ${min_rate} | ${max_rate} | ${threshold}
#| | ... | ${perf_pdr_loss_acceptance} | ${perf_pdr_loss_acceptance_type}

*** Test Cases ***
| tc01-64B-1t1c-eth-l2xcbase-eth-2memif-1vnf-kubernetes-ndrdisc
| | [Documentation]
| | ... | [Cfg] DUT runs L2XC switching config with 1 thread, 1 phy core,\
| | ... | 1 receive queue per NIC port.
| | ... | [Ver] Find NDR for 64 Byte frames using binary search start at 10GE\
| | ... | linerate, step 100kpps.
| | ...
| | [Tags] | 64B | 1T1C | STHREAD | NDRDISC
| | [Template] | L2 Cross Connect Binary Search
| | framesize=${64} | min_rate=${100000} | wt=1 | rxq=1 | search_type=NDR
