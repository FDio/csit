# Copyright (c) 2016 Cisco and/or its affiliates.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at:
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

*** Settings ***
| Resource | resources/libraries/robot/performance.robot
| Library | resources.libraries.python.QemuUtils
| Force Tags | 3_NODE_SINGLE_LINK_TOPO | PERFTEST | HW_ENV | PERFTEST_SHORT
| ...        | PERFTEST_VHOST | NIC_Intel-X520-DA2
| Suite Setup | Run Keywords | 3-node Performance Suite Setup with DUT's NIC model
| ... | L3 | Intel-X520-DA2
| ...         | AND          | Build QEMU on all DUTs
| Suite Teardown | 3-node Performance Suite Teardown
| Test Setup | Run Keywords | Setup all DUTs before test
| ...        | AND          | Qemu kill on all DUTs | ${nodes}
| Test Teardown | Run Keywords | Remove startup configuration of VPP from all DUTs
| ...           | AND          | Show vpp trace dump on all DUTs
| ...           | AND          | Guest VM Teardown | ${dut1}
| ...                          | ${dut1_vm_refs}
| ...           | AND          | Guest VM Teardown | ${dut2}
| ...                          | ${dut2_vm_refs}
| Documentation | *Reference NDR throughput IPv4 routing verify test cases
| ...           | with vhost*
| ...
| ... | *[Top] Network Topologies:* TG-DUT1-DUT2-TG 3-node circular topology
| ... | with single links between nodes.
| ... | *[Enc] Packet Encapsulations:* Eth-IPv4 for IPv4 routing.
| ... | *[Cfg] DUT configuration:* DUT1 and DUT2 are configured with IPv4
| ... | routing and two static IPv4 /24 route entries. Qemu Guest is connected
| ... | to VPP via vhost-user interfaces. Guest is running DPDK testpmd
| ... | interconnecting vhost-user interfaces using 3 cores pinned to cpu mask
| ... | 0xE0 and 2048M. Testpmd is using socket-mem=1024M (512x2M hugepages), 3
| ... | cores (1 main core and 2 cores dedicated for io), forwarding mode is
| ... | set to io, rxq/txq=2048, burst=64. DUT1, DUT2 are tested with 2p10GE
| ... | NIC X520 Niantic by Intel.
| ... | *[Ver] TG verification:* In short performance tests, TG verifies
| ... | DUTs' throughput at ref-NDR (reference Non Drop Rate) with zero packet
| ... | loss tolerance. Ref-NDR value is periodically updated acording to
| ... | formula: ref-NDR = 0.9x NDR, where NDR is found in RFC2544 long
| ... | performance tests for the same DUT configuration. Test packets are
| ... | generated by TG on links to DUTs. TG traffic profile contains two L3
| ... | flow-groups (flow-group per direction, 253 flows per flow-group) with
| ... | all packets containing Ethernet header, IPv4 header with IP protocol=61
| ... | and generated payload. MAC addresses are matching MAC addresses of the
| ... | TG node interfaces.
| ... | *[Ref] Applicable standard specifications:* RFC2544.

*** Variables ***
| ${sock1}= | /tmp/sock-1
| ${sock2}= | /tmp/sock-2

*** Test Cases ***
| TC01: Verify 64B ref-NDR at 2x 0.1Mpps - DUT IPv4-VHOST - 1thread 1core 1rxq
| | [Documentation]
| | ... | [Cfg] DUT runs IPv4 routing config with 1 thread, 1 phy core, \
| | ... | 1 receive queue per NIC port. [Ver] Verify ref-NDR for 64 Byte
| | ... | frames using single trial throughput test.
| | [Tags] | 1_THREAD_NOHTT_RXQUEUES_1 | SINGLE_THREAD | NDR | THIS
| | ${framesize}= | Set Variable | 64
| | ${duration}= | Set Variable | 10
| | ${rate}= | Set Variable | 0.1mpps
| | ${dut1_vm_refs}= | Create Dictionary
| | ${dut2_vm_refs}= | Create Dictionary
| | Given Add '1' worker threads and rxqueues '1' without HTT to all DUTs
| | And   Add PCI devices to DUTs from 3-node single link topology
| | And   Add No Multi Seg to all DUTs
| | And   Apply startup configuration on all VPP DUTs
| | When  IPv4 forwarding with Vhost initialized in a 3-node circular topology
| | ...   | ${sock1} | ${sock2}
| | ${vm1}= | And Guest VM with IP forwarding connected via vhost-user is setup
| | ...     | ${dut1} | ${sock1} | ${sock2} | DUT1_VM1
| | Set To Dictionary | ${dut1_vm_refs} | DUT1_VM1 | ${vm1}
| | ${vm2}= | And Guest VM with IP forwarding connected via vhost-user is setup
| | ...     | ${dut2} | ${sock1} | ${sock2} | DUT2_VM1
| | Set To Dictionary | ${dut2_vm_refs} | DUT2_VM1 | ${vm2}
| | Then  Traffic should pass with no loss | ${duration} | ${rate}
| | ...                                    | ${framesize} | 3-node-IPv4

