# Copyright (c) 2017 Cisco and/or its affiliates.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at:
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

*** Settings ***
| Resource | resources/libraries/robot/performance.robot
| Resource | resources/libraries/robot/ipsec.robot
| Resource | resources/libraries/robot/lisp/lispgpe.robot
| ...
| Variables | resources/test_data/lisp/performance/lisp_static_adjacency.py
| Force Tags | 3_NODE_SINGLE_LINK_TOPO | PERFTEST | HW_ENV | SCALE | NDRPDRDISC
| ... | IP4FWD | LISPGPE | IPSEC | IPSECHW | IPSECTUN | ENCAP | LISP | IP4UNRLAY
| ... | IP4OVRLAY | NIC_Intel-XL710
| ...
| Suite Setup | Run Keywords
| ... | 3-node Performance Suite Setup with DUT's NIC model | L3 | Intel-XL710
| ... | AND | Verify Crypto Device On All DUTs | force_init=${True}
| ... | AND | Verify Kernel Module On All DUTs | igb_uio | force_load=${True}
| Suite Teardown | 3-node Performance Suite Teardown
| ...
| Test Setup | Performance test setup
| Test Teardown | Performance test teardown | ${min_rate}pps | ${framesize}
| ... | 3-node-IPv4
| ...
| Documentation | *IPv4 IPsec tunnel mode performance test suite.*
| ...
| ... | *[Top] Network Topologies:* TG-DUT1-DUT2-TG 3-node circular topology\
| ... | with single links between nodes.
| ... | *[Enc] Packet Encapsulations:* Eth-IPv4 on TG-DUTn,\
| ... | Eth-IPv4-IPSec-LISPGPE-IPv4 on DUT1-DUT2
| ... | *[Cfg] DUT configuration:* Each DUT is configured with LISP and IPsec\
| ... | in each direction. IPsec is in transport mode. DUTs get IPv4 traffic\
| ... | from TG, encrypt it and send to another DUT, where packets are\
| ... | decrypted and sent back to TG.
| ... | *[Ver] TG verification:* TG finds and reports throughput NDR (Non Drop\
| ... | Rate) with zero packet loss tolerance or throughput PDR (Partial Drop\
| ... | Rate) with non-zero packet loss tolerance (LT) expressed in number\
| ... | of packets transmitted. NDR is discovered for different\
| ... | number of IPsec tunnels using binary search algorithms with configured\
| ... | starting rate and final step that determines throughput measurement\
| ... | resolution. Test packets are generated by TG on\
| ... | links to DUTs. TG traffic profile contains two L3 flow-groups\
| ... | (flow-group per direction, number of flows per flow-group equals to\
| ... | number of IPSec tunnels) with all packets\
| ... | containing Ethernet header, IPv4 header with IP protocol=61 and\
| ... | static payload. MAC addresses are matching MAC addresses of the TG\
| ... | node interfaces. Incrementing of IP.dst (IPv4 destination address)\
| ... | field is applied to both streams.
| ... | *[Ref] Applicable standard specifications:* RFC6830, RFC4303 and\
| ... | RFC2544.

*** Variables ***
# XL710-DA2 bandwidth limit ~49Gbps/2=24.5Gbps
| ${s_24.5G} | ${24500000000}
# XL710-DA2 Mpps limit 37.5Mpps/2=18.75Mpps
| ${s_18.75Mpps} | ${18750000}
| ${dut2_spi}= | ${1000}
| ${dut1_spi}= | ${1001}
| ${ipsec_overhead_gcm}= | ${54}

*** Test Cases ***
| tc01-64B-1t1c-ethip4ipsectnllispgpe-ip4base-interfaces-cbc-sha1-ndrdisc
| | [Documentation]
| | ... | [Cfg] Each DUT is configured with LISP and IPsec in each direction.\
| | ... | IPsec is in transport mode. DUTs are configured with 1 thread, 1 phy\
| | ... | core, 1 receive queue per NIC port.
| | ... | [Ver] Find NDR for 64 Byte frames using binary search start at 40GE\
| | ... | linerate, step 100kpps.
| | ...
| | [Tags] | 1T1C | STHREAD | NDRDISC | THIS
| | ...
| | ${n_tunnels}= | Set Variable | ${1}
| | ${framesize}= | Set Variable | ${64}
| | ${min_rate}= | Set Variable | ${10000}
| | ${max_rate}= | Set Variable | ${s_18.75Mpps}
| | ${binary_min}= | Set Variable | ${min_rate}
| | ${binary_max}= | Set Variable | ${max_rate}
| | ${threshold}= | Set Variable | ${min_rate}
| | ${encr_alg}= | Crypto Alg AES CBC 128
| | ${auth_alg}= | Integ Alg SHA1 96
| | Given Add '1' worker threads and rxqueues '1' in 3-node single-link topo
| | And IPsec Generate Keys | ${encr_alg} | ${auth_alg}
| | And Add PCI devices to DUTs from 3-node single link topology
| | And Add No Multi Seg to all DUTs
| | And Add Cryptodev to all DUTs | ${1}
| | And Apply startup configuration on all VPP DUTs
| | When Lisp IPv4 forwarding initialized in a 3-node circular topology
| | ... | ${dut1_to_dut2_ip4} | ${dut1_to_tg_ip4} | ${dut2_to_dut1_ip4}
| | ... | ${dut2_to_tg_ip4} | ${prefix4}
| | And Set up LISP GPE topology
| | ... | ${dut1} | ${dut1_if2} | ${NONE}
| | ... | ${dut2} | ${dut2_if1} | ${NONE}
| | ... | ${duts_locator_set} | ${dut1_ip4_eid} | ${dut2_ip4_eid}
| | ... | ${dut1_ip4_static_adjacency} | ${dut2_ip4_static_adjacency}
| | When VPP Setup IPsec Manual Keyed Connection
| | ... | ${dut1} | ${dut1_if2} | ${encr_alg} | ${encr_key}
| | ... | ${auth_alg} | ${auth_key} | ${dut1_spi} | ${dut2_spi}
| | ... | ${dut1_to_dut2_ip4} | ${dut2_to_dut1_ip4}
| | And VPP Setup IPsec Manual Keyed Connection
| | ... | ${dut2} | ${dut2_if1} | ${encr_alg} | ${encr_key}
| | ... | ${auth_alg} | ${auth_key} | ${dut2_spi} | ${dut1_spi}
| | ... | ${dut2_to_dut1_ip4} | ${dut1_to_dut2_ip4}
| | Then Find NDR using binary search and pps | ${framesize}
| | ... | ${binary_min} | ${binary_max} | 3-node-IPv4
| | ... | ${min_rate} | ${max_rate} | ${threshold}

