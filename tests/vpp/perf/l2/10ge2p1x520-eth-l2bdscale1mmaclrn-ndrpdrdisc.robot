# Copyright (c) 2017 Cisco and/or its affiliates.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at:
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

*** Settings ***
| Resource | resources/libraries/robot/performance/performance_setup.robot
| ...
| Force Tags | 3_NODE_SINGLE_LINK_TOPO | PERFTEST | HW_ENV | NDRPDRDISC
| ... | NIC_Intel-X520-DA2 | ETH | L2BDMACLRN | SCALE | L2BDBASE | FIB_1M
| ...
| Suite Setup | Set up 3-node performance topology with DUT's NIC model
| ... | L2 | Intel-X520-DA2
| Suite Teardown | Tear down 3-node performance topology
| ...
| Test Setup | Set up performance test
| ...
| Test Teardown | Tear down performance discovery test | ${min_rate}pps
| ... | ${framesize} | ${traffic_profile}
| ...
| Test Template | Local template
| ...
| Documentation | *RFC2544: Pkt throughput L2BD test cases*
| ...
| ... | *[Top] Network Topologies:* TG-DUT1-DUT2-TG 3-node circular topology\
| ... | with single links between nodes.
| ... | *[Enc] Packet Encapsulations:* Eth-IPv4 for L2 switching of IPv4.
| ... | *[Cfg] DUT configuration:* DUT1 and DUT2 are configured with L2 bridge-\
| ... | domain and MAC learning enabled. DUT1 and DUT2 tested with 2p10GE NI
| ... | X520 Niantic by Intel.
| ... | *[Ver] TG verification:* TG finds and reports throughput NDR (Non Drop\
| ... | Rate) with zero packet loss tolerance or throughput PDR (Partial Drop\
| ... | Rate) with non-zero packet loss tolerance (LT) expressed in percentage\
| ... | of packets transmitted. NDR and PDR are discovered for different\
| ... | Ethernet L2 frame sizes using either binary search or linear search\
| ... | algorithms with configured starting rate and final step that determines\
| ... | throughput measurement resolution. Test packets are generated by TG on\
| ... | links to DUTs. TG traffic profile contains two L3 flow-groups\
| ... | (flow-group per direction, 500k flows per flow-group) with all packets\
| ... | containing Ethernet header, IPv4 header with IP protocol=61 and static\
| ... | payload. MAC addresses ranges are incremented as follows:
| ... | port01_src ca:fe:00:00:00:00 - port01_src ca:fe:00:07:a1:1f,\
| ... | port01_dst fa:ce:00:00:00:00 - port01_dst fa:ce:00:07:a1:1f,\
| ... | port02_src fa:ce:00:00:00:00 - port02_src fa:ce:00:07:a1:1f,\
| ... | port02_dst ca:fe:00:00:00:00 - port02_dst ca:fe:00:07:a1:1f,\
| ... | *[Ref] Applicable standard specifications:* RFC2544.

*** Variables ***
# X520-DA2 bandwidth limit
| ${s_limit} | ${10000000000}
# Traffic profile:
| ${traffic_profile} | trex-sl-3n-ethip4-macsrc500kdst500k

*** Keywords ***
| Local template
| | [Arguments] | ${framesize} | ${phy_cores} | ${search_type}
| | ... | ${min_rate}=${50000} | ${rxq}=${None}
| | ...
| | Set Test Variable | ${framesize}
| | Set Test Variable | ${min_rate}
| | ${get_framesize}= | Set Variable If
| | ... | "${framesize}" == "IMIX_v4_1" | ${avg_imix_framesize} | ${framesize}
| | ${max_rate}= | Calculate pps | ${s_limit} | ${get_framesize}
| | ${binary_min}= | Set Variable | ${min_rate}
| | ${binary_max}= | Set Variable | ${max_rate}
| | ${threshold}= | Set Variable | ${min_rate}
| | Add worker threads and rxqueues to all DUTs | ${phy_cores} | ${rxq}
| | Add PCI devices to all DUTs
| | Run Keyword If | ${get_framesize} < ${1522} | Add No Multi Seg to all DUTs
| | Apply startup configuration on all VPP DUTs
| | Initialize L2 bridge domain in 3-node circular topology
| | Run Keyword If | '${search_type}' == 'NDR'
| | ... | Find NDR using binary search and pps
| | ... | ${framesize} | ${binary_min} | ${binary_max} | ${traffic_profile}
| | ... | ${min_rate} | ${max_rate} | ${threshold}
| | ... | ELSE IF | '${search_type}' == 'PDR'
| | ... | Find PDR using binary search and pps
| | ... | ${framesize} | ${binary_min} | ${binary_max} | ${traffic_profile}
| | ... | ${min_rate} | ${max_rate} | ${threshold}
| | ... | ${perf_pdr_loss_acceptance} | ${perf_pdr_loss_acceptance_type}

*** Test Cases ***
| tc01-64B-1t1c-eth-l2bdscale1mmaclrn-ndrdisc
| | [Tags] | 64B | 1C | NDRDISC
| | framesize=${64}  | phy_cores=${1} | search_type=NDR

| tc02-64B-1t1c-eth-l2bdscale1mmaclrn-pdrdisc
| | [Tags] | 64B | 1C | PDRDISC | SKIP_PATCH
| | framesize=${64}  | phy_cores=${1} | search_type=PDR

| tc03-1518B-1t1c-eth-l2bdscale1mmaclrn-ndrdisc
| | [Tags] | 1518B | 1C | NDRDISC
| | framesize=${1518}  | phy_cores=${1} | search_type=NDR

| tc04-1518B-1t1c-eth-l2bdscale1mmaclrn-pdrdisc
| | [Tags] | 1518B | 1C | PDRDISC | SKIP_PATCH
| | framesize=${1518}  | phy_cores=${1} | search_type=PDR

| tc05-9000B-1t1c-eth-l2bdscale1mmaclrn-ndrdisc
| | [Tags] | 9000B | 1C | NDRDISC
| | framesize=${9000} | phy_cores=${1} | search_type=NDR

| tc06-9000B-1t1c-eth-l2bdscale1mmaclrn-pdrdisc
| | [Tags] | 9000B | 1C | PDRDISC | SKIP_PATCH
| | framesize=${9000} | phy_cores=${1} | search_type=PDR

| tc07-64B-2t2c-eth-l2bdscale1mmaclrn-ndrdisc
| | [Tags] | 64B | 2C | NDRDISC
| | framesize=${64}  | phy_cores=${2} | search_type=NDR

| tc08-64B-2t2c-eth-l2bdscale1mmaclrn-pdrdisc
| | [Tags] | 64B | 2C | PDRDISC | SKIP_PATCH
| | framesize=${64}  | phy_cores=${2} | search_type=PDR

| tc09-1518B-2t2c-eth-l2bdscale1mmaclrn-ndrdisc
| | [Tags] | 1518B | 2C | NDRDISC | SKIP_PATCH
| | framesize=${1518}  | phy_cores=${2} | search_type=NDR

| tc10-1518B-2t2c-eth-l2bdscale1mmaclrn-pdrdisc
| | [Tags] | 1518B | 2C | PDRDISC | SKIP_PATCH
| | framesize=${1518}  | phy_cores=${2} | search_type=PDR

| tc11-9000B-2t2c-eth-l2bdscale1mmaclrn-ndrdisc
| | [Tags] | 9000B | 2C | NDRDISC | SKIP_PATCH
| | framesize=${9000} | phy_cores=${2} | search_type=NDR

| tc12-9000B-2t2c-eth-l2bdscale1mmaclrn-pdrdisc
| | [Tags] | 9000B | 2C | PDRDISC | SKIP_PATCH
| | framesize=${9000} | phy_cores=${2} | search_type=PDR

| tc13-64B-4t4c-eth-l2bdscale1mmaclrn-ndrdisc
| | [Tags] | 64B | 4C | NDRDISC
| | framesize=${64}  | phy_cores=${4} | search_type=NDR

| tc14-64B-4t4c-eth-l2bdscale1mmaclrn-pdrdisc
| | [Tags] | 64B | 4C | PDRDISC | SKIP_PATCH
| | framesize=${64}  | phy_cores=${4} | search_type=PDR

| tc15-1518B-4t4c-eth-l2bdscale1mmaclrn-ndrdisc
| | [Tags] | 1518B | 4C | NDRDISC | SKIP_PATCH
| | framesize=${1518}  | phy_cores=${4} | search_type=NDR

| tc16-1518B-4t4c-eth-l2bdscale1mmaclrn-pdrdisc
| | [Tags] | 1518B | 4C | PDRDISC | SKIP_PATCH
| | framesize=${1518}  | phy_cores=${4} | search_type=PDR

| tc17-9000B-4t4c-eth-l2bdscale1mmaclrn-ndrdisc
| | [Tags] | 9000B | 4C | NDRDISC | SKIP_PATCH
| | framesize=${9000} | phy_cores=${4} | search_type=NDR

| tc18-9000B-4t4c-eth-l2bdscale1mmaclrn-pdrdisc
| | [Tags] | 9000B | 4C | PDRDISC | SKIP_PATCH
| | framesize=${9000} | phy_cores=${4} | search_type=PDR

