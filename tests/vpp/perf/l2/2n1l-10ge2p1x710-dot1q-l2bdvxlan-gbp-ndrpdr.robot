# Copyright (c) 2020 Cisco and/or its affiliates.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at:
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

*** Settings ***
| Resource | resources/libraries/robot/shared/default.robot
|
| Force Tags | 2_NODE_SINGLE_LINK_TOPO | PERFTEST | HW_ENV | NDRPDR | VXLAN
| ... | NIC_Intel-X710 | DOT1Q | L2BDMACLRN | BASE | DRV_VFIO_PCI | GBP
| ... | dot1q-l2bdvxlan-gbp
|
| Suite Setup | Setup suite single link | performance_avf
| Suite Teardown | Tear down suite | performance
| Test Setup | Setup test
| Test Teardown | Tear down test | performance
|
| Test Template | Local template
|
| Documentation | *RFC2544: Pkt throughput L2BD with IEEE 802.1Q and GBP test
| ... | cases*
|
| ... | *[Top] Network Topologies:* TG-DUT1-TG 2-node circular topology\
| ... | with single links between nodes.
| ... | *[Enc] Packet Encapsulations:* Eth-IPv4 for L2 switching of IPv4 on \
| ... | TG-DUTn. Eth-IPv4-VXLAN-Eth-IPv4 is applied on link between TG-DUT1.\
| ... | IEEE 802.1Q tagging is applied on both links TG-DUT1 .
| ... | *[Cfg] DUT configuration:* DUT1 is configured with:\
| ... | 3 VLAN subinterfaces (VID 200, 300 and 400),\
| ... | VXLAN tunnels are configured between L2BDs on DUT1 and TG\
| ... | 1 L2 BD with the 3 VLAN subinterfaces and a BVI,\
| ... | 1 GBP L3 RD,\
| ... | 1 GBP L2 BD with the L2 BD,\
| ... | 1 GBP EPG EPG-1 with sclass 100, the GBP L2 BD and L3 RD,\
| ... | 2 GBP external EP in EPG-1,\
| ... | 2 external subnets with sclass 200, 300 and 400,\
| ... | Contracts allowing full communications between the 2 external subnets.\
| ... | DUT1 tested with ${nic_name} with VF enabled.
| ... | *[Ver] TG verification:* TG finds and reports throughput NDR (Non Drop\
| ... | Rate) with zero packet loss tolerance and throughput PDR (Partial Drop\
| ... | Rate) with non-zero packet loss tolerance (LT) expressed in percentage\
| ... | of packets transmitted. NDR and PDR are discovered for different\
| ... | Ethernet L2 frame sizes using MLRsearch library.\
| ... | Test packets are generated by TG on\
| ... | links to DUT. TG traffic profile contains two L3 flow-groups\
| ... | (flow-group per direction, 254 flows per flow-group) with all packets\
| ... | containing Ethernet header including IEEE 802.1Q tagging, IPv4 header\
| ... | with IP protocol=61 and static payload. MAC addresses are matching MAC\
| ... | addresses of the TG node interfaces.
| ... | *[Ref] Applicable standard specifications:* RFC2544.

*** Variables ***
| @{plugins_to_enable}= | dpdk_plugin.so | gbp_plugin.so | acl_plugin.so
| ${crypto_type}= | ${None}
| ${nic_name}= | Intel-X710
| ${nic_driver}= | vfio-pci
| ${osi_layer}= | L2
| ${overhead}= | ${4}
# Traffic profile:
| ${traffic_profile}= | trex-sl-ethip4vxlans-ip4src1udpsrcrnd

*** Keywords ***
| Local template
| | [Documentation]
| | ... | [Cfg] DUT runs L2BD with VLAN and GBP config with ${phy_cores}\
| | ... | physical core(s) for worker threads.
| | ... | [Ver] Measure NDR and PDR values using MLRsearch algorithm.\
| |
| | ... | *Arguments:*
| | ... | - frame_size - Framesize in Bytes in integer or string (IMIX_v4_1).
| | ... | Type: integer, string
| | ... | - phy_cores - Number of physical cores. Type: integer
| | ... | - rxq - Number of RX queues, default value: ${None}. Type: integer
| |
| | [Arguments] | ${frame_size} | ${phy_cores} | ${rxq}=${None}
| |
| | Set Test Variable | \${frame_size}
| |
| | Given Set Max Rate And Jumbo
| | And Add worker threads to all DUTs | ${phy_cores} | ${rxq}
| | And Pre-initialize layer driver | ${nic_driver}
| | And Apply Startup configuration on all VPP DUTs
| | When Initialize layer driver | ${nic_driver}
| | And Initialize layer interface
| | And Initialize layer dot1q
| | And Initialize GBP routing domains
| | And Initialize L2 bridge domain with VXLANoIPv4 in 2-node circular topology
| | Then Find NDR and PDR intervals using optimized search

*** Test Cases ***
| tc01-64B-1c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | 64B | 1C
| | frame_size=${64} | phy_cores=${1}

| tc02-64B-2c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | 64B | 2C
| | frame_size=${64} | phy_cores=${2}

| tc03-64B-4c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | 64B | 4C
| | frame_size=${64} | phy_cores=${4}

| tc04-1518B-1c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | 1518B | 1C
| | frame_size=${1518} | phy_cores=${1}

| tc05-1518B-2c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | 1518B | 2C
| | frame_size=${1518} | phy_cores=${2}

| tc06-1518B-4c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | 1518B | 4C
| | frame_size=${1518} | phy_cores=${4}

| tc07-9000B-1c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | 9000B | 1C
| | frame_size=${9000} | phy_cores=${1}

| tc08-9000B-2c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | 9000B | 2C
| | frame_size=${9000} | phy_cores=${2}

| tc09-9000B-4c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | 9000B | 4C
| | frame_size=${9000} | phy_cores=${4}

| tc10-IMIX-1c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | IMIX | 1C
| | frame_size=IMIX_v4_1 | phy_cores=${1}

| tc11-IMIX-2c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | IMIX | 2C
| | frame_size=IMIX_v4_1 | phy_cores=${2}

| tc12-IMIX-4c-dot1q-l2bdvxlan-gbp-ndrpdr
| | [Tags] | IMIX | 4C
| | frame_size=IMIX_v4_1 | phy_cores=${4}
