# Copyright (c) 2017 Cisco and/or its affiliates.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at:
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

*** Settings ***
| Resource | resources/libraries/robot/performance/performance_setup.robot
| ...
| Force Tags | 3_NODE_SINGLE_LINK_TOPO | PERFTEST | HW_ENV | NDRPDRDISC
| ... | NIC_Cisco-VIC-1227 | ETH | L2BDMACLRN | BASE
| ...
| Suite Setup | Set up 3-node performance topology with DUT's NIC model
| ... | L2 | Cisco-VIC-1227
| Suite Teardown | Tear down 3-node performance topology
| ...
| Test Setup | Set up performance test
| ...
| Test Teardown | Tear down performance discovery test | ${min_rate}pps
| ... | ${framesize} | ${traffic_profile}
| ...
| Documentation | *RFC2544: Pkt throughput L2BD test cases*
| ...
| ... | *[Top] Network Topologies:* TG-DUT1-DUT2-TG 3-node circular topology\
| ... | with single links between nodes.
| ... | *[Enc] Packet Encapsulations:* Eth-IPv4 for L2 switching of IPv4.
| ... | *[Cfg] DUT configuration:* DUT1 and DUT2 are configured with L2 bridge-\
| ... | domain and MAC learning enabled. DUT1 and DUT2 tested with 2p10GE NIC
| ... | Cisco-VIC-1227.
| ... | *[Ver] TG verification:* TG finds and reports throughput NDR (Non Drop\
| ... | Rate) with zero packet loss tolerance or throughput PDR (Partial Drop\
| ... | Rate) with non-zero packet loss tolerance (LT) expressed in percentage\
| ... | of packets transmitted. NDR and PDR are discovered for different\
| ... | Ethernet L2 frame sizes using either binary search or linear search\
| ... | algorithms with configured starting rate and final step that determines\
| ... | throughput measurement resolution. Test packets are generated by TG on\
| ... | links to DUTs. TG traffic profile contains two L3 flow-groups\
| ... | (flow-group per direction, 253 flows per flow-group) with all packets\
| ... | containing Ethernet header, IPv4 header with IP protocol=61 and static\
| ... | payload. MAC addresses are matching MAC addresses of the TG node\
| ... | interfaces.
| ... | *[Ref] Applicable standard specifications:* RFC2544.

*** Variables ***
# Cisco-VIC-1227 bandwidth limit
| ${s_limit} | ${10000000000}
# Traffic profile:
| ${traffic_profile} | trex-sl-3n-ethip4-ip4src254

*** Keywords ***
| L2 Bridge Domain NDR Binary Search
| | [Documentation]
| | ... | [Cfg] DUT runs L2BD switching config with with\
| | ... | ${phy_cores} phy core.
| | ... | [Ver] Find NDR for ${framesize}B Byte frames using binary search\
| | ... | start at 10GE linerate, step 50kpps.
| | [Arguments] | ${framesize} | ${phy_cores} | ${min_rate}=${50000}
| | ...
| | Set Test Variable | ${framesize}
| | Set Test Variable | ${min_rate}
| | ${max_rate}= | Calculate pps | ${s_limit} | ${framesize}
| | ${binary_min}= | Set Variable | ${min_rate}
| | ${binary_max}= | Set Variable | ${max_rate}
| | ${threshold}= | Set Variable | ${min_rate}
| | Add worker threads and rxqueues to all DUTs | ${phy_cores}
| | Add PCI devices to all DUTs
| | Run Keyword If | ${framesize} < ${1522} | Add no multi seg to all DUTs
| | Apply startup configuration on all VPP DUTs
| | Initialize L2 bridge domain in 3-node circular topology
| | Find NDR using binary search and pps
| | ... | ${framesize} | ${binary_min} | ${binary_max} | ${traffic_profile}
| | ... | ${min_rate} | ${max_rate} | ${threshold}

| L2 Bridge Domain PDR Binary Search
| | [Documentation]
| | ... | [Cfg] DUT runs L2BD switching config with with\
| | ... | ${phy_cores} phy core.
| | ... | [Ver] Find PDR for ${framesize}B Byte frames using binary search\
| | ... | start at 10GE linerate, step 50kpps.
| | [Arguments] | ${framesize} | ${phy_cores} | ${min_rate}=${50000}
| | ...
| | Set Test Variable | ${framesize}
| | Set Test Variable | ${min_rate}
| | ${max_rate}= | Calculate pps | ${s_limit} | ${framesize}
| | ${binary_min}= | Set Variable | ${min_rate}
| | ${binary_max}= | Set Variable | ${max_rate}
| | ${threshold}= | Set Variable | ${min_rate}
| | Add worker threads and rxqueues to all DUTs | ${phy_cores}
| | Add PCI devices to all DUTs
| | Run Keyword If | ${framesize} < ${1522} | Add no multi seg to all DUTs
| | Apply startup configuration on all VPP DUTs
| | Initialize L2 bridge domain in 3-node circular topology
| | Find PDR using binary search and pps
| | ... | ${framesize} | ${binary_min} | ${binary_max} | ${traffic_profile}
| | ... | ${min_rate} | ${max_rate} | ${threshold}
| | ... | ${perf_pdr_loss_acceptance} | ${perf_pdr_loss_acceptance_type}

*** Test Cases ***
| tc01-64B-1t1c-eth-l2bdbasemaclrn-ndrdisc
| | [Tags] | 64B | 1C | NDRDISC
| | [Template] | L2 Bridge Domain NDR Binary Search
| | framesize=${64}  | phy_cores=${1}

| tc02-64B-1t1c-eth-l2bdbasemaclrn-pdrdisc
| | [Tags] | 64B | 1C | PDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain PDR Binary Search
| | framesize=${64}  | phy_cores=${1}

| tc03-1518B-1t1c-eth-l2bdbasemaclrn-ndrdisc
| | [Tags] | 1518B | 1C | NDRDISC
| | [Template] | L2 Bridge Domain NDR Binary Search
| | framesize=${1518}  | phy_cores=${1}

| tc04-1518B-1t1c-eth-l2bdbasemaclrn-pdrdisc
| | [Tags] | 1518B | 1C | PDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain PDR Binary Search
| | framesize=${1518}  | phy_cores=${1}

| tc05-9000B-1t1c-eth-l2bdbasemaclrn-ndrdisc
| | [Tags] | 9000B | 1C | NDRDISC
| | [Template] | L2 Bridge Domain NDR Binary Search
| | framesize=${9000} | phy_cores=${1}

| tc06-9000B-1t1c-eth-l2bdbasemaclrn-pdrdisc
| | [Tags] | 9000B | 1C | PDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain PDR Binary Search
| | framesize=${9000} | phy_cores=${1}

| tc07-64B-2t2c-eth-l2bdbasemaclrn-ndrdisc
| | [Tags] | 64B | 2C | NDRDISC
| | [Template] | L2 Bridge Domain NDR Binary Search
| | framesize=${64}  | phy_cores=${2}

| tc08-64B-2t2c-eth-l2bdbasemaclrn-pdrdisc
| | [Tags] | 64B | 2C | PDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain PDR Binary Search
| | framesize=${64}  | phy_cores=${2}

| tc09-1518B-2t2c-eth-l2bdbasemaclrn-ndrdisc
| | [Tags] | 1518B | 2C | NDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain NDR Binary Search
| | framesize=${1518}  | phy_cores=${2}

| tc10-1518B-2t2c-eth-l2bdbasemaclrn-pdrdisc
| | [Tags] | 1518B | 2C | PDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain PDR Binary Search
| | framesize=${1518}  | phy_cores=${2}

| tc11-9000B-2t2c-eth-l2bdbasemaclrn-ndrdisc
| | [Tags] | 9000B | 2C | NDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain NDR Binary Search
| | framesize=${9000} | phy_cores=${2}

| tc12-9000B-2t2c-eth-l2bdbasemaclrn-pdrdisc
| | [Tags] | 9000B | 2C | PDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain PDR Binary Search
| | framesize=${9000} | phy_cores=${2}

| tc13-64B-4t4c-eth-l2bdbasemaclrn-ndrdisc
| | [Tags] | 64B | 4C | NDRDISC
| | [Template] | L2 Bridge Domain NDR Binary Search
| | framesize=${64}  | phy_cores=${4}

| tc14-64B-4t4c-eth-l2bdbasemaclrn-pdrdisc
| | [Tags] | 64B | 4C | PDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain PDR Binary Search
| | framesize=${64}  | phy_cores=${4}

| tc15-1518B-4t4c-eth-l2bdbasemaclrn-ndrdisc
| | [Tags] | 1518B | 4C | NDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain NDR Binary Search
| | framesize=${1518}  | phy_cores=${4}

| tc16-1518B-4t4c-eth-l2bdbasemaclrn-pdrdisc
| | [Tags] | 1518B | 4C | PDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain PDR Binary Search
| | framesize=${1518}  | phy_cores=${4}

| tc17-9000B-4t4c-eth-l2bdbasemaclrn-ndrdisc
| | [Tags] | 9000B | 4C | NDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain NDR Binary Search
| | framesize=${9000} | phy_cores=${4}

| tc18-9000B-4t4c-eth-l2bdbasemaclrn-pdrdisc
| | [Tags] | 9000B | 4C | PDRDISC | SKIP_PATCH
| | [Template] | L2 Bridge Domain PDR Binary Search
| | framesize=${9000} | phy_cores=${4}
