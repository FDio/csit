# Copyright (c) 2019 Cisco and/or its affiliates.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at:
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

*** Settings ***
| Resource | resources/libraries/robot/shared/default.robot
| Resource | resources/libraries/robot/ip/nat.robot
| Resource | resources/libraries/robot/shared/traffic.robot
|
| Force Tags | 2_NODE_SINGLE_LINK_TOPO | DEVICETEST | HW_ENV | DCR_ENV | SCAPY
| ... | NIC_Virtual | ETH | IP4FWD | FEATURE | NAT44 | BASE | DRV_VFIO_PCI
|
| Suite Setup | Setup suite single link | scapy
| Suite Teardown | Tear down suite
| Test Setup | Setup test
| Test Teardown | Tear down test | packet_trace
|
| Test Template | Local Template
|
| Documentation | *NAT44 test cases*
|
| ... | *[Top] Network Topologies:* TG-DUT1-TG 2-node circular topology \
| ... | with single links between nodes.
| ... | *[Enc] Packet Encapsulations:* Eth-IPv4-UDP for IPv4 routing.
| ... | *[Cfg] DUT configuration:* DUT1 is configured with IPv4 routing and \
| ... | one static IPv4 /18 route entries. DUT1 is tested with ${nic_name}.
| ... | *[Ver] TG verification:* Test packets are generated by TG on \
| ... | links to DUT1. TG traffic profile contains two L3 flow-groups \
| ... | (flow-group per direction, 1 flow per flow-group) with all packets \
| ... | containing Ethernet header, IPv4 header with IP protocol=17 and \
| ... | on receive TG verifies packet for correctness of respective header.
| ... | *[Ref] Applicable standard specifications:*

*** Variables ***
| @{plugins_to_enable}= | dpdk_plugin.so | nat_plugin.so
| ${crypto_type}= | ${None}
| ${nic_name}= | virtual
| ${nic_driver}= | vfio-pci
| ${osi_layer}= | L3
| ${overhead}= | ${0}
| ${p1_src_port}= | 1024
| ${p1_dst_port}= | 1024
| ${p2_src_port}= | 1024
| ${p2_dst_port}= | 1028

*** Keywords ***
| Local Template
| |
| | [Documentation]
| | ... | [Ver] Make TG send Eth-IPv4-UDP packet routed over DUT1 interfaces. \
| | ... | Make TG verify Eth-IPv4-UDP packet is correct.
| |
| | ... | *Arguments:*
| | ... | - frame_size - Framesize in Bytes in integer. Type: integer
| | ... | - phy_cores - Number of physical cores. Type: integer
| | ... | - rxq - Number of RX queues, default value: ${None}. Type: integer
| |
| | [Arguments] | ${frame_size} | ${phy_cores} | ${rxq}=${None}
| |
| | Set Test Variable | \${frame_size}
| |
| | Given Set Max Rate And Jumbo
| | And Add worker threads to all DUTs | ${phy_cores} | ${rxq}
| | And Pre-initialize layer driver | ${nic_driver}
| | And Add NAT to all DUTs
| | And Apply startup configuration on all VPP DUTs
| | When Initialize layer driver | ${nic_driver}
| | And Initialize layer interface
| | And Initialize NAT44 in circular topology
| | Then Send TCP or UDP packet and verify received packet | ${tg}
| | ... | 20.0.0.0 | 12.0.0.2
| | ... | ${tg_if1} | ${tg_if1_mac}
| | ... | ${tg_if2} | ${dut1_if1_mac}
| | ... | UDP | ${p1_src_port} | ${p1_dst_port}
| | Then Send TCP or UDP packet and verify received packet | ${tg}
| | ... | 12.0.0.2 | 200.0.0.0
| | ... | ${tg_if2} | ${tg_if2_mac}
| | ... | ${tg_if1} | ${dut1_if2_mac}
| | ... | UDP | ${p2_src_port} | ${p2_dst_port}

*** Test Cases ***
| tc01-64B-1c-ethip4udp-ip4base-nat44-dev
| | [Tags] | 64B
| | frame_size=${64} | phy_cores=${0}
