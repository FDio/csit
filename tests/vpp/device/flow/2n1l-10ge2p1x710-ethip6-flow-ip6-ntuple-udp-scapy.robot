# Copyright (c) 2021 Intel and/or its affiliates.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at:
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

*** Settings ***
| Resource | resources/libraries/robot/shared/default.robot
|
| Force Tags | 2_NODE_SINGLE_LINK_TOPO | DEVICETEST | HW_ENV | DCR_ENV | SCAPY
| ... | NIC_Intel-X710 | ETH | IP6FWD | FLOW | NTUPLE | UDP | DRV_VFIO_PCI
| ... | RXQ_SIZE_0 | TXQ_SIZE_0
| ... | ethip6-flow-ip6-ntuple-udp
|
| Suite Setup | Setup suite topology interfaces | scapy
| Test Setup | Setup test
| Test Teardown | Tear down test | packet_trace | telemetry
|
| Test Template | Local Template
|
| Documentation | *IP6_N_TUPLE flow test cases*
| ...
| ... | *[Top] Network Topologies:* TG-DUT1 2-node topology with one link\
| ... | between nodes.
| ... | *[Enc] Packet Encapsulations:* Eth-IP6-UDP.
| ... | *[Cfg] DUT configuration:* DUT is configured with IP6_N_TUPLE flow.
| ... | *[Ver] TG verification:* Verify if the flow action is correct.

*** Variables ***
| @{plugins_to_enable}= | dpdk_plugin.so | perfmon_plugin.so
| ${crypto_type}= | ${None}
| ${nic_name}= | Intel-X710
| ${nic_driver}= | vfio-pci
| ${nic_rxq_size}= | 0
| ${nic_txq_size}= | 0
| ${nic_pfs}= | 2
| ${nic_vfs}= | 0
| ${overhead}= | ${0}
| ${src_ip}= | 2001:0db8:3c4d:0015:0000:0000:1a2f:1a2b
| ${dst_ip}= | 1011:2022:3033:4044:5055:6066:7077:8088
| ${src_port}= | ${100}
| ${dst_port}= | ${200}
| ${rxq}= | ${4}
# Telemetry
| ${telemetry_profile}= | vpp_test_teardown

*** Keywords ***
| Local Template
| | [Documentation]
| | ... | [Ver] Make TG send IP6 packet routed over DUT1 interfaces.\
| | ... | Make VPP verify flow packet is correct.
| |
| | ... | *Arguments:*
| | ... | - frame_size - Framesize in Bytes in integer. Type: integer
| | ... | - phy_cores - Number of physical cores. Type: integer
| | ... | - rxq - Number of RX queues. Type: integer
| |
| | [Arguments] | ${frame_size} | ${phy_cores} | ${rxq}=${rxq}
| |
| | Set Test Variable | \${frame_size}
| |
| | Given Set Max Rate And Jumbo
| | And Add worker threads to all DUTs | ${phy_cores} | ${rxq}
| | And Pre-initialize layer driver | ${nic_driver}
| | And Apply startup configuration on all VPP DUTs | with_trace=${True}
| | When Initialize layer driver | ${nic_driver}
| | And Initialize layer interface
| |
| | Clear Packet Trace On Dut | ${dut1}
| | Vpp Enable Traces On Dut | ${dut1}
| | ${flow_index} = | And Vpp Create Ip6 N Tuple Flow | ${dut1}
| | ... | ${src_ip} | ${dst_ip} | ${src_port} | ${dst_port}
| | ... | proto=UDP | action=redirect-to-queue | value=${${rxq}-1}
| | And Vpp Flow Enable | ${dut1} | ${DUT1_${int}1}[0] | ${flow_index}
| | Then Send flow packet and verify action
| | ... | ${tg} | ${TG_pf1}[0] | ${DUT1_${int}1_mac}[0]
| | ... | flow_type=IP6 | proto=UDP
| | ... | src_ip=${src_ip} | dst_ip=${dst_ip}
| | ... | src_port=${src_port} | dst_port=${dst_port}
| | ... | action=redirect-to-queue | action_value=${${rxq}-1}
| | And Vpp Flow Disable | ${dut1} | ${DUT1_${int}1}[0] | ${flow_index}
| | And Vpp Flow Del | ${dut1} | ${flow_index}
| |
| | Clear Packet Trace On Dut | ${dut1}
| | Vpp Enable Traces On Dut | ${dut1}
| | ${flow_index} = | And Vpp Create Ip6 N Tuple Flow | ${dut1}
| | ... | ${src_ip} | ${dst_ip} | ${src_port} | ${dst_port}
| | ... | proto=UDP | action=drop
| | And Vpp Flow Enable | ${dut1} | ${DUT1_${int}1}[0] | ${flow_index}
| | Then Send flow packet and verify action
| | ... | ${tg} | ${TG_pf1}[0] | ${DUT1_${int}1_mac}[0]
| | ... | flow_type=IP6 | proto=UDP
| | ... | src_ip=${src_ip} | dst_ip=${dst_ip}
| | ... | src_port=${src_port} | dst_port=${dst_port}
| | ... | action=drop
| | And Vpp Flow Disable | ${dut1} | ${DUT1_${int}1}[0] | ${flow_index}
| | And Vpp Flow Del | ${dut1} | ${flow_index}
| |
| | Clear Packet Trace On Dut | ${dut1}
| | Vpp Enable Traces On Dut | ${dut1}
| | ${flow_index} = | And Vpp Create Ip6 N Tuple Flow | ${dut1}
| | ... | ${src_ip} | ${dst_ip} | ${src_port} | ${dst_port}
| | ... | proto=UDP | action=mark | value=${7}
| | And Vpp Flow Enable | ${dut1} | ${DUT1_${int}1}[0] | ${flow_index}
| | Then Send flow packet and verify action
| | ... | ${tg} | ${TG_pf1}[0] | ${DUT1_${int}1_mac}[0]
| | ... | flow_type=IP6 | proto=UDP
| | ... | src_ip=${src_ip} | dst_ip=${dst_ip}
| | ... | src_port=${src_port} | dst_port=${dst_port}
| | ... | action=mark
| | And Vpp Flow Disable | ${dut1} | ${DUT1_${int}1}[0] | ${flow_index}
| | And Vpp Flow Del | ${dut1} | ${flow_index}

*** Test Cases ***
| 64B-0c-ethip6-flow-ip6-ntuple-udp-scapy
| | [Tags] | 64B | 0C
| | frame_size=${64} | phy_cores=${0}
